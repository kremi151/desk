plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'io.gitlab.arturbosch.detekt'
    id 'maven-publish'
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 23
        targetSdk 31
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'lu.kremi151'
            artifactId = 'desk'
            version = rootProject.isRelease
                ? rootProject.versionName
                : "${rootProject.versionName}-SNAPSHOT"

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            name = 'githubPackages'
            url = "https://maven.pkg.github.com/kremi151/desk"
            credentials(PasswordCredentials)
        }
    }
}

def detektTask = tasks.named("detekt")
detektTask.configure {
    reports {
        xml {
            enabled = true
        }
    }
}

dependencies {
}
